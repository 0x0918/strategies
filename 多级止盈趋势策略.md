
> 策略名称

多级止盈趋势策略

> 策略作者

littleDreamXX

> 策略描述

- 策略名称: 多级止盈趋势策略
- 数据周期：多周期
- 支持：商品期货、数字货币
- 官方网站：www.quant.la

![IMG](https://www.fmz.com/upload/asset/b8d89a704605b72bc44a56575a570bf6.png)

- 主图：
  上线，公式 ：UPPERLINE^^今开+BAND;
  下线，公式 ：LOWERLINE^^今开-BAND;
  均线，公式 ：均线^^MA(CLOSE,LENGTH);

- 副图：
  无

> 策略参数



|参数|默认值|描述|
|----|----|----|
|LENGTH|200|均线指标参数|
|STOPRANGE|3|止损幅度|


> 源码 (麦语言)

``` pascal
(*backtest
start: 2018-01-01 00:00:00
end: 2018-04-09 00:00:00
period: 1d
exchanges: [{"eid":"Bitfinex","currency":"BTC_USD"}]
args: [["LENGTH",80]]
*)

STARTPER1:=5;  //1级跟踪止盈，盈利5%启动
STOPPER1:=100; //1级跟踪止盈，盈利回撤100%触发    
STARTPER2:=10; //2级跟踪止盈，盈利10%启动
STOPPER2:=50;  //2级跟踪止盈，盈利回撤50%触发   
STARTPER3:=20; //3级跟踪止盈，盈利20%启动
STOPPER3:=20;  //3级跟踪止盈，盈利回撤20%触发
    
//上下区间
NN:=BARSLAST(DATE<>REF(DATE,1))+1;//今天开盘到目前为止的周期数
今开:=VALUEWHEN(NN=1,O);//当天的开盘价
今高:=HHV(H,NN);//今天全天的最高价
今低:=LLV(L,NN);//今天全天的最低价
昨高:=REF(今高,NN);//昨天全天的最高价
昨低:=REF(今低,NN);//昨天全天的最低价

BAND:=昨高-昨低;
UPPERLINE^^今开+BAND;
LOWERLINE^^今开-BAND;
    
//均线
均线^^MA(CLOSE,LENGTH);
            
//开平仓条件
卖开条件: = C<LOWERLINE AND LOWERLINE<均线;
买开条件: = C>UPPERLINE AND UPPERLINE>均线;
买平空单: = C>UPPERLINE OR C>均线;
卖平多单: = C<LOWERLINE OR C<均线;

//程序主体
//开仓
卖开条件,SK;
买开条件,BK;
//平仓
卖平多单,SP;
买平空单,BP;

//止盈        
SKLOW<=SKPRICE*(1-0.01*STARTPER3) AND HIGH>=SKLOW+(SKPRICE-SKLOW)*0.01*STOPPER3,BP;//最大盈利达到"STARTPER3"%之后盈利回撤"STOPPER3"%,平空
BKHIGH>=BKPRICE*(1+0.01*STARTPER3) AND LOW<=BKHIGH-(BKHIGH-BKPRICE)*0.01*STOPPER3,SP;//最大盈利达到"STARTPER3"%之后盈利回撤"STOPPER3"%,平多
SKLOW<=SKPRICE*(1-0.01*STARTPER2) AND HIGH>=SKLOW+(SKPRICE-SKLOW)*0.01*STOPPER2,BP;//最大盈利达到"STARTPER2"%之后盈利回撤"STOPPER2"%,平空
BKHIGH>=BKPRICE*(1+0.01*STARTPER2) AND LOW<=BKHIGH-(BKHIGH-BKPRICE)*0.01*STOPPER2,SP;//最大盈利达到"STARTPER2"%之后盈利回撤"STOPPER2"%,平多
SKLOW<=SKPRICE*(1-0.01*STARTPER1) AND HIGH>=SKLOW+(SKPRICE-SKLOW)*0.01*STOPPER1,BP;//最大盈利达到"STARTPER1"%之后盈利回撤"STOPPER1"%,平空
BKHIGH>=BKPRICE*(1+0.01*STARTPER1) AND LOW<=BKHIGH-(BKHIGH-BKPRICE)*0.01*STOPPER1,SP;//最大盈利达到"STARTPER1"%之后盈利回撤"STOPPER1"%,平多

//止损
C>=SKPRICE*(1+STOPRANGE*0.01),BP;//空头止损
C<=BKPRICE*(1-STOPRANGE*0.01),SP;//多头止损

SETSIGPRICETYPE(BK,NEW_ORDER);
SETSIGPRICETYPE(SK,NEW_ORDER);
SETSIGPRICETYPE(BP,NEW_ORDER);
SETSIGPRICETYPE(SP,NEW_ORDER);

AUTOFILTER;
```

> 策略出处

https://www.fmz.com/strategy/128122

> 更新时间

2018-12-03 11:41:22
