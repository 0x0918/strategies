
> 策略名称

三轨道波动率策略

> 策略作者

littleDreamXX

> 策略描述

- K线走完下单：收盘价开仓、平仓
- 数据合约：螺纹指数，郑煤指数，热卷指数，铁矿指数，焦炭指数，棕榈指数，橡胶指数，郑醇指数，PP指数，沪铜指数；
- 交易合约：螺纹主连，郑煤主连，热卷主连，铁矿主连，焦炭主连，棕榈主连，橡胶主连，郑醇主连，PP主连，沪铜主连；
- 数据级别：日K线；

![IMG](https://www.fmz.com/upload/asset/785c3a0e4e8c164bc831b05dcdc355a3.png)

- 主图指标显示：
   MIDTR^^MA(C,CN);                                        // 确定MIDTR
   UPTR^^MIDTR+2*STD(C,CN);                       // 确定UPTR
   DOWNTR^^MIDTR-2*STD(C,CN);                  // 确定DOWNTR
   HPOINT^^HV(H,CN),DOT,COLORRED;          // 计算前一周期CN周期内最高价的最大值。
   LPOINT^^LV(L,CN),DOT,COLORBLUE;          // 计算前一周期CN周期内最低价的最小值。

- 副图：
  无

> 策略参数



|参数|默认值|描述|
|----|----|----|
|AMOUNT|true|单仓|
|N|20|标准差周期数|
|MINN|20|CN最小值|
|MAXN|60|CN最大值|
|STOPRANGE|50|止损幅度|


> 源码 (麦语言)

``` pascal
(*backtest
start: 2017-07-01 00:00:00
end: 2018-11-30 00:00:00
period: 1d
exchanges: [{"eid":"Futures_CTP","currency":"FUTURES"}]
*)

// 确定CN 
VOLAT:=STD(C,N);                            // VOLAT（波动率）:M周期收盘价的标准差
VOLATCHANGE:=(VOLAT-REF(VOLAT,1))/VOLAT;    // 2个VOLAT的变化率
N1:=(1+VOLATCHANGE)*MINN;                   // VOLATCHANGE ： 波动率变化
N2:=INTPART(N1);                            // 取整
N3:=MIN(N2,MAXN);                           // 确认CN不大于60
CN:=MAX(N3,MINN);                           // 确认CN不小于20

MIDTR^^MA(C,CN);                            // 确定MIDTR
UPTR^^MIDTR+2*STD(C,CN);                    // 确定UPTR
DOWNTR^^MIDTR-2*STD(C,CN);                  // 确定DOWNTR
HPOINT^^HV(H,CN),DOT,COLORRED;              // 计算前一周期CN周期内最高价的最大值。
LPOINT^^LV(L,CN),DOT,COLORBLUE;             // 计算前一周期CN周期内最低价的最小值。

// 开仓
L<=LPOINT AND L<DOWNTR AND BARPOS>MINN,SK(AMOUNT);  // 当最低价小于DOWNTR和低点，且K线位置大于60，收盘价卖开
H>=HPOINT AND H>UPTR AND BARPOS>MINN,BK(AMOUNT);    // 当最高价大于UPTR和高点，且K线位置大于60，收盘价买开

// 启动止损
C>=SKPRICE*(1+STOPRANGE*0.001),BP(SKVOL);
C<=BKPRICE*(1-STOPRANGE*0.001),SP(BKVOL);

// 平仓
C<MIDTR,SP(BKVOL);                             // 当收盘价小于MIDTR，收盘价卖平
C>MIDTR,BP(SKVOL);                             // 当收盘价大于MIDTR，收盘价买平

// 动态止损
REF(BKHIGH,1)>BKPRICE*(1+2*0.001*STOPRANGE) AND C<HV(C,BARSBK)*(1-STOPRANGE*0.001),SP(BKVOL);  // 买开后最高价大于买开价*(1+2*0.001*STOPRANGE)，且收盘价小于买开后最高收盘价*(1-STOPRANGE*0.001)，收盘价卖平
REF(SKLOW,1)<SKPRICE*(1-2*0.001*STOPRANGE) AND C>LV(C,BARSSK)*(1+STOPRANGE*0.001),BP(SKVOL);   // 卖开后最低价小于卖开价*(1-2*0.001*STOPRANGE)，且收盘价大于卖开后最低收盘价*(1+STOPRANGE*0.001)，收盘价买平
```

> 策略出处

https://www.fmz.com/strategy/128129

> 更新时间

2018-12-03 09:52:12
