
> 策略名称

高低点突破成交量指数加权策略

> 策略作者

littleDreamXX

> 策略描述

- 策略名称：高低点突破成交量指数加权策略
- 数据周期：多周期
- 回测可以选择 OKEX期货
- 合约：this_week 当周合约
- 官方网站：www.quant.la

/upload/asset/efa8dad9db6b13862283a69ee8255934.png

- 主图：
  无

- 副图
  VJQ， 计算公式： VJQ:EMA(V*(C-REF(C,NC)),N);//定义成交量加权指数为VJQ

> 策略参数



|参数|默认值|描述|
|----|----|----|
|SLOSS|2|止损百分比|
|N|300|EMA指标参数|
|NC|50|多少个周期前的收盘价|
|MINAMOUNT|true|最少一次下单数量|


> 源码 (麦语言)

``` pascal
(*backtest
start: 2018-04-01 00:00:00
end: 2018-05-28 00:00:00
period: 1h
exchanges: [{"eid":"Futures_OKCoin","currency":"BTC_USD"}]
args: [["N",100],["MINAMOUNT",10],["TradeAmount",10,126961],["ContractType","this_week",126961]]
*)

LOTS:=MAX(MINAMOUNT,INTPART(MONEYTOT/O * 0.8));
VJQ:EMA(V*(C-REF(C,NC)),N);//定义成交量加权指数为VJQ
B:=VJQ>0;//定义多头势
S:=VJQ<0;//定义空头势
BUYPK:=BARPOS>N AND BKVOL=0 AND B AND H>=HHV(H,N);
SELLPK:=BARPOS>N AND SKVOL=0 AND S AND L<=LLV(L,N);
BUYP:=SKVOL>0 AND B;
SELLP:=BKVOL>0 AND S;

//入场
SELLPK,SPK(LOTS);
BUYPK,BPK(LOTS);

//出场
BUYP,BP(SKVOL);
SELLP,SP(BKVOL);

//止损
C>=SKPRICE*(1+SLOSS*0.01),BP(SKVOL);
C<=BKPRICE*(1-SLOSS*0.01),SP(BKVOL);
AUTOFILTER;
```

> 策略出处

https://www.fmz.com/strategy/128125

> 更新时间

2018-12-05 10:57:07
