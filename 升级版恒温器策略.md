
> 策略名称

升级版恒温器策略

> 策略作者

littleDreamXX

> 策略描述

- 策略名称：升级版恒温器策略
- 数据周期：1H
- 支持：商品期货
- 官方网站：WWW.QUANT.LA

   ![IMG](https://www.fmz.com/upload/asset/bcf664987bac1aaff72110b1e3679d10.png)

- 主图：
  上轨，公式：TOP^^MAC+N_TMP*TMP;//布林通道上轨
  下轨，公式：BOTTOM^^MAC-N_TMP*TMP;//布林通道下轨

- 副图：
  CMI，公式 ： CMI:ABS(C-REF(C,N_CMI-1))/(HHV(H,N_CMI)-LLV(L,N_CMI))*100;//0-100 取值越大，说明趋势越强，CMI<20震荡模式，CMI>20为趋势

> 策略参数



|参数|默认值|描述|
|----|----|----|
|STOPLOSS|true|止损价格百分比|
|N|50|布林线指标参数1|
|N_TMP|2|布林线指标参数2|


> 源码 (麦语言)

``` pascal
(*backtest
start: 2018-11-06 00:00:00
end: 2018-12-04 00:00:00
period: 1h
exchanges: [{"eid":"Futures_CTP","currency":"FUTURES"}]
*)

MAC:=MA(CLOSE,N);
TMP:=STD(CLOSE,N);
TOP^^MAC+N_TMP*TMP;//布林通道上轨
BOTTOM^^MAC-N_TMP*TMP;//布林通道下轨
BBOLL:=C>MAC;
SBOLL:=C<MAC;
N_CMI:=30;

CMI:ABS(C-REF(C,N_CMI-1))/(HHV(H,N_CMI)-LLV(L,N_CMI))*100;//0-100 取值越大，说明趋势越强，CMI<20震荡模式，CMI>20为趋势

N_KD:=9;
M1:=3;
M2:=3;
RSV:=(CLOSE-LLV(LOW,N_KD))/(HHV(HIGH,N_KD)-LLV(LOW,N_KD))*100;//收盘价与N周期最低值做差，N周期最高值与N周期最低值做差，两差之间做比值。
K:=SMA(RSV,M1,1);//RSV的移动平均值
D:=SMA(K,M2,1);//K的移动平均值
MIND:=30;
BKD:=K>D AND D<MIND;
SKD:=K<D AND D>100-MIND;

//震荡模式
BUYPK1:=CMI < 20 AND BKD;//震荡多单买平开
SELLPK1:=CMI < 20 AND SKD;//震荡空单卖平开

//趋势模式下原有震荡持仓的处理
SELLY1:=REF(CMI,BARSBK) < 20 AND C>BKPRICE*(1+0.01*STOPLOSS*3) AND K<D;//震荡多单止盈
BUYY1:=REF(CMI,BARSSK) < 20 AND C<SKPRICE*(1-0.01*STOPLOSS*3) AND K>D;//震荡空单止盈

//趋势模式
BUYPK2:=CMI >= 20 AND C > TOP;//趋势多单买平开
SELLPK2:=CMI >= 20 AND C < BOTTOM;//趋势空单卖平开

//趋势模式下原有震荡持仓的处理
SELLY2:=REF(CMI,BARSBK) >= 20 AND C>BKPRICE*(1+0.01*STOPLOSS*3) AND SBOLL;//趋势多单止盈
BUYY2:=REF(CMI,BARSSK) >= 20 AND C<SKPRICE*(1-0.01*STOPLOSS*3) AND BBOLL;//趋势空单止盈

SELLS2:=REF(CMI,BARSBK) >= 20 AND C<BKPRICE*(1-0.01*STOPLOSS) AND SBOLL;//趋势多单止损
BUYS2:=REF(CMI,BARSSK) >= 20 AND C>SKPRICE*(1+0.01*STOPLOSS) AND BBOLL;//趋势空单止损

IF BARPOS>N THEN BEGIN
    BUYPK1,BPK;
    SELLPK1,SPK;
    BUYPK2,BPK;
    SELLPK2,SPK;
END
BUYY1,BP(SKVOL);
BUYY2,BP(SKVOL);
BUYS2,BP(SKVOL);
SELLY1,SP(BKVOL);
SELLY2,SP(BKVOL);
SELLS2,SP(BKVOL);


```

> 策略出处

https://www.fmz.com/strategy/129086

> 更新时间

2018-12-06 13:39:54
